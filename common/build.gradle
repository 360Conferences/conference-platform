plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.kotlin.native.cocoapods'
}

group 'com.conferenceplatform'
version '0.0.1' // CocoaPods requires the podspec to have a version.

repositories {
    mavenCentral()
}

android {
    compileSdkVersion = buildConfig.compileSdk
    defaultConfig {
        minSdkVersion = buildConfig.minSdk
        targetSdkVersion = buildConfig.targetSdk
        versionCode = buildConfig.version.code
        versionName = buildConfig.version.name
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
}

kotlin {

    js() {
        browser {}
    }
    jvm()
    android()
    // Revert to just ios() once the gradle plugin can properly resolve this
    if (System.getenv('SDK_NAME')?.startsWith('iphoneos')) {
        iosArm64('ios') {
            binaries.framework('model')
        }
    } else {
        iosX64('ios') {
            binaries.framework('model')
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation deps.kotlin.stdlib.common
            }
        }
        commonTest {
            dependencies {
                implementation deps.kotlin.test.common
                implementation deps.kotlin.test.annotations
            }
        }
        jvmMain {
            dependencies {
                implementation deps.kotlin.stdlib.jdk
            }
        }
        jvmTest {
            dependencies {
                implementation deps.kotlin.test.jdk
            }
        }
        androidMain {
            dependencies {
                implementation deps.kotlin.stdlib.jdk
            }
        }
        androidTest {
            dependencies {
                implementation deps.kotlin.test.jdk
            }
        }
        jsMain {
            dependencies {
                implementation deps.kotlin.stdlib.js
            }
        }
        jsTest {
            dependencies {
                implementation deps.kotlin.test.js
            }
        }
        iosMain {}
        iosTest {}
    }

    cocoapods {
        // Configure fields required by CocoaPods.
        summary = "Conference Platform iOS App"
        homepage = "https://github.com/360Conferences/conference-platform"
    }
}

configurations {
    compileClasspath
}